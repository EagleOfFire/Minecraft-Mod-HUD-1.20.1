plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.hidetake.ssh' version '2.12.0'
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: mapping_channel, version: mapping_version

    copyIdeResources = true

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', mod_id

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', mod_id

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        gameTestServer {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', mod_id

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/public/' }

    flatDir {
        dirs 'libs'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    compileOnly 'org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT'
    compileOnly files('libs/MagicSpells-4.0-Beta-14_NATHOX.jar')
    compileOnly files('libs/bukkit-plugin-1.0-SNAPSHOT.jar')
    implementation 'org.yaml:snakeyaml:2.2'
}

shadowJar {
    // On embarque seulement snakeyaml (ou d'autres libs, mais pas Minecraft)
    dependencies {
        include(dependency('org.yaml:snakeyaml'))
    }
    // Surtout, ne fais pas from configurations.runtimeClasspath (sauf si filtrÃ©)
}


def resourceTargets = ['META-INF/mods.toml', 'pack.mcmeta']
def replaceProperties = [
        minecraft_version   : minecraft_version, minecraft_version_range: minecraft_version_range,
        forge_version       : forge_version, forge_version_range: forge_version_range,
        loader_version_range: loader_version_range,
        mod_id              : mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
        mod_authors         : mod_authors, mod_description: mod_description
]

processResources {
    inputs.properties replaceProperties
    replaceProperties.put 'project', project

    filesMatching(resourceTargets) {
        expand replaceProperties
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_authors,
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

ext.privateConfigFile = file 'private.properties'
privateConfigFile.withReader {
    def p = new Properties()
    p.load(it)
    project.ext.privateConfig = new ConfigSlurper().parse p
}

import org.hidetake.groovy.ssh.core.Remote

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.hidetake:groovy-ssh:2.12.0'
}

remotes {
    deployServer{
        host = privateConfig.ftp_adress
        user = privateConfig.ftp_user
        port = privateConfig.ftp_port as int
        password = privateConfig.ftp_pw
    }
}

ssh.settings {
    knownHosts = allowAnyHosts
}

tasks.register("uploadJarSftp"){
    dependsOn("jar")

    doLast {
        def libsDir = layout.buildDirectory.dir("libs").get().asFile

        def candidates = fileTree(libsDir).matching {
            include "*.jar"
            exclude "*-source.jar", "*-javadoc.jar", "*-dev.jar"
        }.files.toList().sort {a, b -> b.lastModified() <=> a.lastModified()}

        if (candidates.isEmpty()) {
            throw new GradleException("No jar found in ${libsDir}. Did the build produce a jar ?")
        }

        def jarFile = candidates[0]
        def remoteDir = privateConfig.ftp_remote_dir ?: "."

        logger.lifecycle("Uploading ${jarFile.name} -> ${privateConfig.ftp_adress}:${remoteDir}")

        ssh.run {
            session(remotes.deployServer as Remote){
                put from: jarFile, into: remoteDir
            }
        }
    }
}